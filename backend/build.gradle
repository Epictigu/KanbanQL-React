import org.springframework.boot.gradle.tasks.run.BootRun

plugins {
    id 'java'
    id 'org.springframework.boot' version '3.0.1'
    id 'io.spring.dependency-management' version '1.1.0'
}

group = 'de.fhswf.kanbanql'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '17'

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }

    localH2Implementation.extendsFrom implementation
    localH2RuntimeOnly.extendsFrom runtimeOnly
}

sourceSets {
    localH2 {
        compileClasspath += sourceSets.main.runtimeClasspath
        runtimeClasspath += sourceSets.main.runtimeClasspath
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-data-rest'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-webflux'

    implementation group: 'com.google.guava', name: 'guava', version: '31.1-jre'
    implementation 'org.springframework.boot:spring-boot-starter-graphql'
    implementation group: 'org.springframework.boot', name: 'spring-boot-starter-rsocket', version: '3.1.2'


    compileOnly 'org.projectlombok:lombok'
    developmentOnly 'org.springframework.boot:spring-boot-devtools'
    localH2RuntimeOnly 'com.h2database:h2'
    runtimeOnly 'com.mysql:mysql-connector-j'
    annotationProcessor 'org.projectlombok:lombok'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.1'
    testImplementation group: 'org.junit.platform', name: 'junit-platform-launcher', version: '1.9.2'
    testImplementation group: 'org.mockito', name: 'mockito-junit-jupiter', version: '5.3.0'
    testImplementation 'com.h2database:h2'
    testImplementation 'org.springframework.graphql:spring-graphql-test'
    testImplementation 'io.projectreactor:reactor-test'
}

configurations.localH2Implementation {
    exclude group: 'com.mysql', module: 'mysql-connector-j'
}

tasks.named("test") {
    useJUnitPlatform()
    systemProperty 'spring.profiles.active', 'dev'
}

tasks.register("runBackend", BootRun.class) {
    group = "kanban-ql"

    setMainClass "de.fhswf.kanbanql.KanbanQLApplication"
    classpath = sourceSets.localH2.runtimeClasspath
    systemProperty 'spring.profiles.active', 'dev'
}
